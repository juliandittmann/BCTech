// KQL samples - Company lifecycle events (create/initialize/copy/delete)

// See this section in the performance tuning guide
// https://learn.microsoft.com/en-us/dynamics365/business-central/dev-itpro/performance/performance-application#be-cautious-with-the-copy-company-operation

// create company
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'LC0001'
| where customDimensions.eventId == 'LC0001' // starting from version 16.1, the eventId is used to identity signal types
| project aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, result = customDimensions.result // Success, Failure, Canceled
, sqlExecutes = customDimensions.sqlExecutes
, sqlRowsRead = customDimensions.sqlRowsRead
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)


// initialize company
// Introduced in version 22.0
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'AL0000EIW'
| where customDimensions.eventId == 'AL0000EIW'
| project aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, isEvaluationCompany = customDimensions.alIsEvaluationCompany
, tenantLicenseState = customDimensions.alTenantLicenseState
, userRole = customDimensions.alUserRole
, userIsAdmin = customDimensions.alIsAdmin
, usertelemetryId = user_Id


// copy company
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'LC0002'
| where customDimensions.eventId == 'LC0002' // starting from version 16.1, the eventId is used to identity signal types
| project aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyNameSource = customDimensions.companyNameSource
, companyNameDestination = customDimensions.companyNameDestination
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, result = customDimensions.result // Success, Failure, Canceled
, sqlExecutes = customDimensions.sqlExecutes
, sqlRowsRead = customDimensions.sqlRowsRead
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)


// delete company
traces
| where timestamp > ago(60d) // adjust as needed
| where customDimensions has 'LC0003'
| where customDimensions.eventId == 'LC0003' // starting from version 16.1, the eventId is used to identity signal types
| project aadTenantId = customDimensions.aadTenantId
, environmentName = customDimensions.environmentName
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, serverExecutionTime = customDimensions.serverExecutionTime
, serverExecutionTimeInMS = toreal(totimespan(customDimensions.serverExecutionTime))/10000 //the datatype for executionTime is timespan 
, totalTime = customDimensions.totalTime
, totalTimeInMS = toreal(totimespan(customDimensions.totalTime))/10000 //the datatype for executionTime is timespan 
, result = customDimensions.result // Success, Failure, Canceled
, sqlExecutes = customDimensions.sqlExecutes
, sqlRowsRead = customDimensions.sqlRowsRead
, usertelemetryId = case(
  // user telemetry id was introduced in the platform in version 20.0
  toint( substring(customDimensions.componentVersion,0,2)) >= 20, user_Id
, 'N/A'
)